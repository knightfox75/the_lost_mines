1 ' THE LOST MINES
2 ' (c)1996-2015 Cesar Rincon
3 '
4 ' Program Init
5 CLEAR512:MAXFILES=1:DEFINTA-Z
6 A=RND(-TIME)
7 DEFUSR1=&H41:DEFUSR2=&H44
8 DIMCL(5):CL(0)=1:CL(1)=4:CL(2)=5:CL(3)=7:CL(4)=14:CL(5)=15
9 DIMPW$(12),TM(12),ND(12),MR(12),X1(12),X2(12),Y1(12),Y2(12)
10 DATA"QWERTYUI",60,234,136,3,3,14,15
11 DATA"ASDFGHJK",60,242,137,3,21,18,11
12 DATA"ZXCVBNMQ",80,228,138,25,3,3,3
13 DATA"QAZWSXED",70,236,139,21,5,9,5
14 DATA"EDCRFVTG",80,98,137,3,3,3,21
15 DATA"YHNUJMIK",40,223,139,3,3,4,21
16 DATA"ZSEXDRCF",60,238,136,25,21,3,3
17 DATA"VGYBHUNJ",70,251,138,9,4,9,20
18 DATA"XSWCDEVF",60,105,139,17,20,4,20
19 DATA"VFRBGTNH",60,197,136,18,5,20,20
20 DATA"XDRVGYNJ",70,229,139,13,3,14,13
21 RESTORE 10:FORI=1TO11:READPW$(I),TM(I),ND(I),MR(I),X1(I),Y1(I),X2(I),Y2(I):NEXT
22 PW$(12)="GAMEOVER"
23 GS=50
24 SCREEN1,3:KEYOFF:WIDTH32:COLOR14,1,1:CLS
25 POKE&HF3DB,0:POKE&HFCAB,1:OUT&HAA,90
26 DATA255,130,191,131,251,251,131,255,255,130,190,190,190,190,130,255,255,8,187,187,184,187,187,255,255,248,251,249,248,251,8,255
27 DATA255,32,175,175,40,174,160,255,255,32,238,238,224,238,46,255,254,130,190,158,142,190,130,254,254,130,186,130,174,182,186,254
28 RESTORE26
29 FORC=1TO2
30 A$="":FORA=1TO32:READB:A$=A$+CHR$(B):NEXT:SPRITE$(C)=A$
31 NEXTC
32 HS=2500
33 ONSTOPGOSUB34:STOPON:GOTO36
34 STOPOFF:CLOSE#1:RETURN33
35 ' Game Init
36 SOUND7,184:SOUND8,10:SOUND9,10:SOUND10,10:SOUND0,0:SOUND1,0:SOUND2,0:SOUND3,0:SOUND4,0:SOUND5,0
37 SCREEN1:A=USR1(0):COLOR1,1,1:CLS:A=USR2(0)
38 GOSUB87
39 GOSUB204
40 SC=0:GP=0:GE=0:TT=0:FS=0
41 ' Game Control
42 IFST=12THEN248
43 IFST=1THENRESTORE273
44 IFST=2THENRESTORE295
45 IFST=3THENRESTORE317
46 IFST=4THENRESTORE339
47 IFST=5THENRESTORE361
48 IFST=6THENRESTORE383
49 IFST=7THENRESTORE405
50 IFST=8THENRESTORE427
51 IFST=9THENRESTORE449
52 IFST=10THENRESTORE471
53 IFST=11THENRESTORE493
54 TM=TM(ST):ND=ND(ST):MR=MR(ST):X1=X1(ST):A1=X1:X2=X2(ST):A2=X2:Y1=Y1(ST):B1=Y1:Y2=Y2(ST):B2=Y2:GM=0:CM=1:W=1:P$="a"
55 A$="STAGE"+STR$(ST):X=FIX(16-(LEN(A$)/2)):LOCATEX,8:PRINTA$:FORA=0TO5:COLORCL(A):GOSUB267:NEXTA:GOSUB270:FORA=5TO0STEP-1:COLORCL(A):GOSUB267:NEXTA:COLOR1,1,1:CLS
56 GOSUB261:COLOR14,1,1:GOSUB95:A=USR2(0)
57 SOUND0,0:SOUND1,0:SOUND2,0:SOUND3,0:SOUND4,0:SOUND5,0
58 GOSUB270:SOUND4,100:FORA=1TO250:NEXTA:SOUND4,50:GOSUB269:SOUND4,0
59 ' Game play
60 A=RND(-TIME):TIME=0
61 GOSUB101
62 GOSUB119
63 GOSUB134
64 GOSUB139
65 GOTO61
66 ' Init VRAM (forms)
67 DATA64,60,66,189,161,161,189,66,60:'@
68 DATA97,255,195,165,129,165,153,195,255:'a
69 DATA98,255,195,153,165,129,165,195,255:'b
70 DATA99,255,195,169,133,133,169,195,255:'c
71 DATA100,255,195,149,161,161,149,195,255:'d
72 DATA104,255,239,199,131,193,227,247,255:'h
73 DATA112,255,239,199,131,193,227,247,255:'p
74 DATA120,129,60,32,48,48,32,60,129:'x
75 DATA121,129,60,32,48,48,32,60,129:'y
76 DATA128,0,0,0,0,0,0,0,0:'
77 DATA136,15,240,15,240,15,240,15,240:'
78 DATA137,51,204,51,204,51,204,51,204:'
79 DATA138,85,170,85,170,85,170,85,170:'
80 DATA139,152,27,196,147,188,47,94,243:'
81 DATA140,15,240,15,240,15,240,15,240:'
82 DATA141,51,204,51,204,51,204,51,204:'
83 DATA142,85,170,85,170,85,170,85,170:'
84 DATA143,152,27,196,147,188,47,94,243:'
85 DATA144,102,165,195,0,0,195,165,102:'
86 DATA152,255,181,211,135,225,203,173,255:'
87 RESTORE67
88 FORA=1TO20
89 READB:B=B*8
90 FORC=BTOB+7:READD:VPOKEC,D:NEXTC
91 NEXTA
92 RETURN
93 ' Init VRAM (color)
94 DATA8204,10,8205,7,8206,4,8207,12,8209,6,8210,13,8211,3
95 RESTORE94
96 FORC=1TO7
97 READA:READB:VPOKEA,B
98 NEXTC
99 RETURN
100 ' Player
101 K=STICK(Q)
102 ONKGOTO104,103,105,103,106,103,107,103
103 RETURN
104 Y1=Y1-1:P$="b":GOTO108
105 X1=X1+1:P$="c":GOTO108
106 Y1=Y1+1:P$="a":GOTO108
107 X1=X1-1:P$="d"
108 A=6144+X1+(Y1*32):S=VPEEK(A)
109 IFS=128THEN116
110 IFS=104THENSC=SC+1:GM=GM+1:GP=GP+1:GOTO116
111 IFS=MRTHENX1=A1:Y1=B1:RETURN
112 IFS=112THENGOSUB143:IFW=-1THENSC=SC-25:GOTO116ELSESC=SC+25:GOTO116
113 IFS=120ANDGM=NDTHENRETURN180
114 IFS=120ANDGM<>NDTHENX1=A1:Y1=B1:RETURN
115 IFS=144ORS=152THENRETURN160
116 SOUND0,120:LOCATEA1,B1:PRINT"":LOCATEX1,Y1:PRINTP$:SOUND0,0
117 A1=X1:B1=Y1:RETURN
118 ' Enemy
119 ONCMGOTO120,121,122,123
120 X2=X2+1:GOTO124
121 X2=X2-1:GOTO124
122 Y2=Y2-1:GOTO124
123 Y2=Y2+1
124 A=6144+X2+(Y2*32):S=VPEEK(A)
125 IFS=128THEN131
126 IFS=104THENSC=SC-1:GM=GM+1:GE=GE+1:GOTO131
127 IFS=MRTHENX2=A2:Y2=B2:CM=FIX(RND(TIME)*4)+1:RETURN
128 IFS=112THENGOSUB143:GOTO131
129 IFS>96ANDS<101THENRETURN160
130 X2=A2:Y2=B2:CM=FIX(RND(TIME)*4)+1:RETURN
131 SOUND2,60:LOCATEA2,B2:PRINT"":LOCATEX2,Y2:PRINT"":SOUND2,0
132 A2=X2:B2=Y2:RETURN
133 ' Print Score
134 LOCATE24,0:PRINTUSING"#####";SC
135 IFSC>HSTHENHS=SCELSERETURN
136 LOCATE10,0:PRINTUSING"#####";HS
137 RETURN
138 ' Timer
139 IFTIME<GSTHENRETURN
140 TM=TM-1:TT=TT+1:LOCATE29,10:PRINTUSING"##";TM
141 IFTM=0THENRETURN162ELSETIME=0:RETURN
142 ' Morph effect
143 Z=TIME:W=-W:A=MR*8
144 IFW=-1THENFORB=1TO3:FORC=ATOA+7:D=FIX(RND(TIME)*256):VPOKEC,D:SOUND4,D:NEXTC:NEXTB:SOUND4,0:FORB=ATOA+7:VPOKEB,255:NEXTB:TIME=Z:RETURN
145 IFMR=136THENRESTORE77
146 IFMR=137THENRESTORE78
147 IFMR=138THENRESTORE79
148 IFMR=139THENRESTORE80
149 READA:A=A*8:FORB=1TO3:FORC=ATOA+7:D=FIX(RND(TIME)*256):VPOKEC,D:SOUND4,D:NEXTC:NEXTB:SOUND4,0:FORB=ATOA+7:READC:VPOKEB,C:NEXTB:TIME=Z:RETURN
150 ' Game Over
151 DATA0,0,0,24,24,0,0,0
152 DATA0,0,24,60,60,24,0,0
153 DATA0,24,60,126,126,60,24,0
154 DATA0,60,126,126,126,126,60,0
155 DATA60,126,255,255,255,255,126,60
156 DATA60,126,255,231,231,255,126,60
157 DATA60,126,231,195,195,231,126,60
158 DATA60,66,129,129,129,129,66,60
159 DATA0,0,0,0,0,0,0,0
160 LOCATEA1,B1:PRINT"":LOCATEX1,Y1:PRINTP$
161 LOCATEA2,B2:PRINT"":LOCATEX2,Y2:PRINT""
162 RESTORE151
163 FORA=1032TO1039:VPOKEA,0:NEXTA:LOCATEX1,Y1:PRINT""
164 FORA=1TO9:SOUND4,50+(20*A):FORB=1032TO1039:READC:VPOKEB,C:NEXTB:GOSUB268:NEXTA:SOUND4,0
165 GOSUB269
166 IFW=-1THENGOSUB143:GOSUB269
167 LOCATE10,5:PRINT"            "
168 LOCATE10,6:PRINT" GAME  OVER "
169 LOCATE10,7:PRINT"            "
170 LOCATE10,8:PRINT"            "
171 LOCATE10,9:PRINT" -PASSWORD- "
172 LOCATE10,10:PRINT"            "
173 LOCATE10,11:PRINT"  "+PW$(ST)+"  "
174 LOCATE10,12:PRINT"            "
175 IFSTRIG(Q)<>0THEN175
176 IFSTRIG(Q)<>-1THEN176
177 FORA=5TO0STEP-1:COLORCL(A):GOSUB267:NEXTA:COLOR1,1,1:CLS:GOSUB269
178 GOTO36
179 ' Stage Clear
180 LOCATEA1,B1:PRINT"":LOCATEX1,Y1:PRINTP$
181 IFW=-1THENGOSUB269:GOSUB143
182 GOSUB270
183 FORA=0TO112:PUTSPRITE1,(-16+A,-32+A),15,1:PUTSPRITE2,(240-A,192-A),15,2:NEXTA
184 GOSUB268:FORA=5TO0STEP-1:PUTSPRITE1,(96,80),CL(A),1:PUTSPRITE2,(128,80),CL(A),2:GOSUB267:NEXTA
185 FORA=0TO5:PUTSPRITE1,(96,80),CL(A),1:PUTSPRITE2,(128,80),CL(A),2:GOSUB267:NEXTA
186 GOSUB270
187 SC=SC+ND(ST):SOUND4,50:GOSUB134:SOUND4,0:GOSUB269
188 SC=SC+(ST*25):SOUND4,50:GOSUB134:SOUND4,0:GOSUB269
189 SC=SC+(TM*5):SOUND4,50:GOSUB134:SOUND4,0:GOSUB269
190 TM=TM-1:SC=SC+1:SOUND4,50:GOSUB134:LOCATE29,10:PRINTUSING"##";TM:SOUND4,0
191 GOSUB267:IFTM<>0THEN190
192 GOSUB270
193 FORA=5TO0STEP-1:COLORCL(A):PUTSPRITE1,(96,80),CL(A),1:PUTSPRITE2,(128,80),CL(A),2:GOSUB267:NEXTA:COLOR1,1,1:PUTSPRITE1,(0,192),1,0:PUTSPRITE2,(0,192),1,0:CLS
194 FS=FS+1:LOCATE3,0:PRINT"STAGE";ST;"CLEAR":LOCATE5,4:PRINT"CLEARED STAGES =>";FS:LOCATE5,7:PRINT"GEMS COLLECTED":LOCATE7,9:PRINT"PLAYER =>";GP:LOCATE7,11:PRINT"ENEMY =>";GE:LOCATE5,14:PRINT"PLAYING TIME => ";
195 A$="":A=TT\60:IFA<10THENA$=A$+"0"+STR$(A)+":"ELSEA$=A$+STR$(A)+":"
196 A=TTMOD60:IFA<10THENA$=A$+"0"+STR$(A)ELSEA$=A$+STR$(A)
197 B$="":FORA=1TOLEN(A$):C$=MID$(A$,A,1):IFC$=" "THENNEXTAELSEB$=B$+C$:NEXTA
198 PRINTB$:LOCATE5,17:PRINT"HI-SCORE =>";HS:LOCATE5,19:PRINT"PLAYER SCORE =>";SC
199 FORA=0TO5:COLORCL(A):GOSUB267:NEXTA
200 IFSTRIG(Q)<>0THEN200
201 IFSTRIG(Q)=0THEN201
202 FORA=5TO0STEP-1:COLORCL(A):GOSUB267:NEXTA:COLOR1,1,1:CLS:ST=ST+1:GOTO43
203 ' Title
204 GOSUB267
205 A$="THE LOST MINES":A=LEN(A$):X=FIX(16-(A/2)):Y=3:GOSUB209
206 A$="PRESS SPACE OR JOY BUTTON":A=LEN(A$):X=FIX(16-(A/2)):Y=10:GOSUB209
207 A$="@ 1996-2015 BY CESAR RINCON":A=LEN(A$):X=FIX(16-(A/2)):Y=20:GOSUB209
208 FORA=0TO5:COLORCL(A):GOSUB267:NEXTA:COLOR15,1,1:GOTO210
209 LOCATEX,Y:PRINTA$:RETURN
210 IFSTRIG(0)<>0ORSTRIG(1)<>0ORSTRIG(2)<>0THEN210
211 IFSTRIG(0)=-1THENQ=0:GOTO215
212 IFSTRIG(1)=-1THENQ=1:GOTO215
213 IFSTRIG(2)=-1THENQ=2:GOTO215
214 GOTO211
215 FORA=5TO0STEP-1:COLORCL(A):GOSUB267:NEXTA:COLOR1,1,1:CLS
216 A$="NEW GAME":A=LEN(A$):X=FIX(16-(A/2)):Y=5:GOSUB209
217 A$="CONTINUE":A=LEN(A$):X=FIX(16-(A/2)):Y=8:GOSUB209
218 FORA=0TO5:COLORCL(A):GOSUB267:NEXTA:COLOR15,1,1:GOSUB95
219 A=1:LOCATE10,5:PRINT"a"
220 K=STICK(Q)
221 IFK=1ANDA=2THENA=1:SOUND4,100:LOCATE10,8:PRINT" ":LOCATE10,5:PRINT"a":SOUND4,0
222 IFK=5ANDA=1THENA=2:SOUND4,100:LOCATE10,5:PRINT" ":LOCATE10,8:PRINT"a":SOUND4,0
223 IFSTRIG(Q)=-1THEN225
224 GOTO220
225 IFA=1THENST=1:GOTO246
226 LOCATE10,8:PRINT" "
227 A$="ENTER PASSWORD":A=LEN(A$):X=FIX(16-(A/2)):Y=12:GOSUB209
228 X=12:Y=14:LOCATEX,Y:PRINT"h"
229 B$=""
230 FORA=1TO8
231 A$=INKEY$:IFA$<>""THEN231
232 A$=INKEY$:IFA$=""THEN232
233 IFASC(A$)=32THEN235
234 IFASC(A$)<65ORASC(A$)>90THEN232
235 B$=B$+A$:SOUND4,80:LOCATEX,Y:PRINTA$:X=X+1:LOCATEX,Y:PRINT"h":SOUND4,0
236 NEXTA
237 GOSUB 269
238 IFB$="SHOWCODE"THENFORA=5TO0STEP-1:COLORCL(A):GOSUB267:NEXTA:SCREEN0:COLOR14,1,1:CLS:POKE&HFCAB,0:END
239 IFB$="SLOWDOWN"THENGS=60
240 IFB$="MYTURTLE"THENGS=500
241 ST=0
242 FORA=1TO12
243 IFB$=PW$(A)THENST=A
244 NEXTA
245 IFST=0THENFORA=5TO0STEP-1:COLORCL(A):GOSUB267:NEXTA:COLOR1,1,1:CLS:GOTO204
246 FORA=5TO1STEP-1:COLORCL(A):GOSUB267:NEXTA:COLOR1,1,1:CLS:RETURN
247 ' Game Over
248 COLOR1,1,1:CLS
249 A$="CONGRATULATIONS!":Y=4:GOSUB257:A$="YOU HAVE EXPLORED ALL MINES":Y=6:GOSUB257:A$="YOU ARE THE BEST ADVENTURER":Y=8:GOSUB257:IFFS<>ST-1THENA$="...NEXT TIME DO NOT CONTINUE":Y=11:GOSUB257:GOSUB258ELSEGOSUB258
250 GOSUB271:GOSUB259
251 A$="THE LOST MINES":Y=8:GOSUB257:A$="TEAM":Y=10:GOSUB257:GOSUB258:GOSUB271:GOSUB259
252 A$="CODE, GFX, SOUND AND DESIGN":Y=8:GOSUB257:A$="CESAR RINCON":Y=10:GOSUB257:GOSUB258:GOSUB271:GOSUB259
253 A$="THANKS TO":Y=6:GOSUB257:A$="JUDITH AND BENJAMI RINCON":Y=8:GOSUB257
254 A$="JORDI GONZALEZ":Y=9:GOSUB257:A$="ERIC BOEZ":Y=10:GOSUB257:A$="JOSE LUIS TUR":Y=11:GOSUB257:GOSUB258:GOSUB271:GOSUB259
255 A$="PRESENTED BY":Y=8:GOSUB257:A$="NIGHTFOX AND CO":Y=10:GOSUB257:GOSUB258:GOSUB271:GOSUB259
256 GOSUB269:GOTO36
257 X=FIX(16-(LEN(A$)/2)):LOCATEX,Y:PRINTA$:RETURN
258 FORA=0TO5:COLORCL(A):GOSUB267:NEXTA:RETURN
259 FORA=5TO0STEP-1:COLORCL(A):GOSUB267:NEXTA:COLOR1,1,1:CLS:RETURN
260 ' Stage Draw subroutine
261 A=USR1(0)
262 FORY=2TO22:READA$:LOCATE2,Y:PRINTA$:NEXTY
263 LOCATE2,0:PRINT"HISCORE":LOCATE18,0:PRINT"SCORE":LOCATE28,8:PRINT"TIME"
264 LOCATE10,0:PRINTUSING"#####";HS:LOCATE24,0:PRINTUSING"#####";SC:LOCATE29,10:PRINTUSING"##";TM
265 RETURN
266 ' Delays
267 FORZZ=1TO50:NEXT:RETURN
268 FORZZ=1TO100:NEXT:RETURN
269 FORZZ=1TO500:NEXT:RETURN
270 FORZZ=1TO1000:NEXT:RETURN
271 FORZZ=1TO5000:NEXT:RETURN
272 ' stage 01
273 DATA""
274 DATA"ahhhhhhhhhhhhhhhhhhhp"
275 DATA"hhhhhhh"
276 DATA"hhhhhhhhhhhhhh"
277 DATA"hhhhhh"
278 DATA"hhhhhhhhhhhhhhhhhhhh"
279 DATA"hhhhxhh"
280 DATA"hhhhhhhhhhhhhh"
281 DATA"hhhhhhhhhhhhhh"
282 DATA"hhhhhhhhh"
283 DATA"hhhhhhhhhh"
284 DATA"hhhhhhhhhhhhhhhhhh"
285 DATA"hhhhhhhh"
286 DATA"hhhhhhhhhhhhhh"
287 DATA"hhhhhhhhhph"
288 DATA"hhhhhhhhhhh"
289 DATA"hhhhhhhhhhhhhhh"
290 DATA"hhhhhhhhhhhhhh"
291 DATA"hhhhh"
292 DATA"phhhhhhhhhhhhhhhhhhhhp"
293 DATA""
294 ' stage 02
295 DATA""
296 DATA"hhhhhhhhhhhhhhhhhhh"
297 DATA"hhhhhhhhhhhhhhhhh"
298 DATA"hhhhphhhhhhhhhh"
299 DATA"hhhhhhh"
300 DATA"hhhhhhhhhhh"
301 DATA"phhhhhhh"
302 DATA"hhhhhhhhhhhhhhhh"
303 DATA"hhhhhhhhhh"
304 DATA"hhhhhhhhxhhhhh"
305 DATA"hhhhhhhhhhhhh"
306 DATA"hhhhhhhhhhhh"
307 DATA"hhhhhhhhhhhh"
308 DATA"hhhhhhhhhhhhh"
309 DATA"hhhhhhhhhhhhh"
310 DATA"hhhhhhhhhhh"
311 DATA"hhhhhhhhhhhhh"
312 DATA"hhhhhhhhhhhhhh"
313 DATA"hhhhhhh"
314 DATA"ahhhhhhhhhhhhhhhhhhhh"
315 DATA""
316 ' stage 03
317 DATA""
318 DATA"hhhhhhphhhhhhhhphhhha"
319 DATA"hhhh"
320 DATA"hhhhhhhhhhhhhhhhhhx"
321 DATA"hhhh"
322 DATA"hhhhhhhhhhhhhhhhh"
323 DATA"hhhhhh"
324 DATA"hhhhhhhhhhhhph"
325 DATA"hhhhhh"
326 DATA"hhhhhhhhhhhhhh"
327 DATA"hhhhhhhhhh"
328 DATA"hhhhhhhhhhhhhhh"
329 DATA"hhhhhhhhhhhh"
330 DATA"hhhhhhhhhhhhhhh"
331 DATA"hhhhhhhhh"
332 DATA"hhhhhhhphhhhhhh"
333 DATA"hhhhhhhhhhh"
334 DATA"hhhhhhhhhhhhhphhh"
335 DATA"hhhh"
336 DATA"phhhhhhhhhhhhhhhhhhhhhh"
337 DATA""
338 ' stage 04
339 DATA""
340 DATA"hhhhhhhhhhhhhhhhhhhhhh"
341 DATA"hhhhp"
342 DATA"hhhhhhhhhhhhhhah"
343 DATA"hhhhhhhhhhhh"
344 DATA"hhhhhhhhhhhhhhh"
345 DATA"hhhhhhhhhhhhhh"
346 DATA"hhhhhhhhhhh"
347 DATA"hhhhhhhhhhhh"
348 DATA"hhhhhhhhhhhhh"
349 DATA"hhhhhhhhhhhh"
350 DATA"hhhhhhhhhhhhhhh"
351 DATA"hhhhhhhhh"
352 DATA"hhhhhhhhhhhhhhh"
353 DATA"hhhhhhhhh"
354 DATA"hhhxhhhhhhhh"
355 DATA"hhhhhhhhhh"
356 DATA"hhhhhhhhhhhhhhh"
357 DATA"hhh"
358 DATA"hhhhhhhhhhhhhhhhhhh"
359 DATA""
360 ' stage 06
361 DATA""
362 DATA"ahhhhhhx"
363 DATA"hhhhhhhh"
364 DATA"hhhhhhhh"
365 DATA"hhhhhhhh"
366 DATA"hhhhhhhh"
367 DATA"hhhhhhhhhhhh"
368 DATA"hhhhhhhhhhhhhh"
369 DATA"hhhhhhhhhhhh"
370 DATA"hh"
371 DATA"h"
372 DATA"h"
373 DATA"hp"
374 DATA"h"
375 DATA"h"
376 DATA"h"
377 DATA"h"
378 DATA"h"
379 DATA"hhhhhhh"
380 DATA"hhhhhp"
381 DATA""
382 ' stage 08
383 DATA""
384 DATA"ahphhhhhhhhhhhhhhhhhp"
385 DATA"hh"
386 DATA"hhhhphhhhhhhhhhhhhph"
387 DATA"hhhhhh"
388 DATA"hhhhphhhhhhhhhphh"
389 DATA"hhhhhhhh"
390 DATA"hhhhhphhhhhphhh"
391 DATA"hhhhhhhhhh"
392 DATA"hhhhhhxhhhh"
393 DATA"hhhhhhhhhhh"
394 DATA"hhhhhhhhhhhhh"
395 DATA"hhhhhhhhhhh"
396 DATA"hhhhhhhhhhhhh"
397 DATA"hhhhhhhhhhh"
398 DATA"hhhhhhhhhhhhh"
399 DATA"hhhhhhhhhhh"
400 DATA"hhhhhhhhhhhhh"
401 DATA"hhhhhhhppph"
402 DATA"hhhhhhhhhhhhhhhhhhhp"
403 DATA""
404 ' stage 09
405 DATA""
406 DATA"hhhhhhhhhhhhhhhhhhhh"
407 DATA"hhhh"
408 DATA"hhhhhhhhhhhhhhhh"
409 DATA"hhhhhhhhh"
410 DATA"hhhhhhph"
411 DATA"hhhhhhhhhhhhhhhhhhh"
412 DATA"hhhh"
413 DATA"phhhhhhhhhhhhhhhhhhhh"
414 DATA"hhhhhhh"
415 DATA"hhhhhhhhhhhhhhhhhhh"
416 DATA"hhhhhhhhhh"
417 DATA"hhhhhhhhhhhhhhhhh"
418 DATA"hhhhhhhh"
419 DATA"hhhhhhhhhhhhhhhhhhhh"
420 DATA"hhhhhhh"
421 DATA"hhhhhhhhhhhhhhhhhh"
422 DATA"hhhxhhhhhh"
423 DATA"hhhhhh"
424 DATA"phhhhhhhhhhhhhhhhhpha"
425 DATA""
426 ' stage 11
427 DATA""
428 DATA"phhhhhhhhhhhhhhhhhhhhp"
429 DATA"hhahhhhhhh"
430 DATA"hhhhhhhhhh"
431 DATA"hhhhhhhhhhhphhhhhhhhhhh"
432 DATA"hhhh"
433 DATA"hhhhhhhhhhhhhhhhhhhhhhh"
434 DATA"hhhh"
435 DATA"hhhhhhhhhhhhhhhhhh"
436 DATA"hhhhhh"
437 DATA"hhhhhhhhhhhhhhhhhhhh"
438 DATA"hhhhhh"
439 DATA"hhhhhhhhhhhhhhhhhh"
440 DATA"hhhh"
441 DATA"hhhhhhhhhhhhhhhhhhhhhhh"
442 DATA"hhhh"
443 DATA"hhhhhhhhhhhphhhhhhhhhhh"
444 DATA"hhhhhhhhhh"
445 DATA"hhhhhhxhh"
446 DATA"phhhhhhhhhhhhhhhhhhhhp"
447 DATA""
448 ' stage 12
449 DATA""
450 DATA"x"
451 DATA"hhh"
452 DATA""
453 DATA"hhh"
454 DATA"hhh"
455 DATA"hh"
456 DATA"hhh"
457 DATA"hh"
458 DATA"hhh"
459 DATA"hh"
460 DATA"hhhhhhhhhhhh"
461 DATA"hhhhh"
462 DATA"hhhhhhhhhh"
463 DATA"hhhhhhhhhhh"
464 DATA"hhhhhh"
465 DATA"hhhhhhhhhhhhhhhhhhhh"
466 DATA"h"
467 DATA"hhhhhhhhhhhhahhhhhhh"
468 DATA""
469 DATA""
470 ' stage 13
471 DATA""
472 DATA"phhhhhhhhhhhhhhhhhhhhhp"
473 DATA"hhxh"
474 DATA"hyhah"
475 DATA"hhhhhhhhhhhhhhhhhhhh"
476 DATA"hhhhh"
477 DATA"hhhhhhhhhhhhhhhhhhhhhh"
478 DATA"hhh"
479 DATA"hhh"
480 DATA"hhhhhhhhhhhhhhhhhhhhhh"
481 DATA"hhhh"
482 DATA"hhhh"
483 DATA"hhhhhhhhhhhhhhhhhhhhh"
484 DATA"hhhh"
485 DATA"hhhhhhhh"
486 DATA"hhhhhhhhhhhhhhhhhhh"
487 DATA"hhhh"
488 DATA"hhhyh"
489 DATA"hyhhhhhhh"
490 DATA"phhhhhhhhhhhhhhhhhhhp"
491 DATA""
492 ' stage 15
493 DATA""
494 DATA"hphhhhhhhahhhhhhhhhhp"
495 DATA"hhhhhhhhh"
496 DATA"hhhhhhhhhh"
497 DATA"hhhhhhhhhhhhhphhh"
498 DATA"hhhhhhhh"
499 DATA"hhhhhhhhhhhhh"
500 DATA"hhhyhhhhhhhhhh"
501 DATA"hhhhhhh"
502 DATA"hhhhhhhhphhyhhhhh"
503 DATA"hhhhh"
504 DATA"hhhhhhhhhhhhhhhhphhhhh"
505 DATA"hhhhhh"
506 DATA"hphhhhhhhhhhhhhyph"
507 DATA"hhhhhhhh"
508 DATA"hhhhhhhhhhhhhhh"
509 DATA"hhhphhhhh"
510 DATA"hhhhhhhhhhhhhhhhhh"
511 DATA"hhhhhh"
512 DATA"phhhhhhhxhhhhhhhhhhhp"
513 DATA""
514 ' Version 1.4
